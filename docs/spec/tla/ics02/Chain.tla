---------------------------- MODULE Chain ----------------------------

EXTENDS Integers, FiniteSets, IBCCoreDefinitions, 
        ICS02ClientHandlers
        
CONSTANTS MaxHeight, \* maximal chain height
          ChainID \* chain identifier

VARIABLES chainStore, \* chain store, containing client heights
          incomingDatagrams \* set of incoming datagrams

vars == <<chainStore, incomingDatagrams>>
Heights == 1..MaxHeight \* set of possible heights of the chains in the system          

(***************************************************************************
 Client update operators
 ***************************************************************************)
\* Update the clients on chain with chainID, 
\* using the client datagrams generated by the relayer      
\* (Handler operators defined in ICS02ClientHandlers.tla)
LightClientUpdate(chainID, store, datagrams) == 
    \* create clients
    LET clientCreatedStore == HandleCreateClient(chainID, store, datagrams) IN
    \* update clients
    LET clientUpdatedStore == HandleClientUpdate(chainID, clientCreatedStore, datagrams) IN

    clientUpdatedStore

(***************************************************************************
 Chain update operators
 ***************************************************************************)
\* Update chainID with the received datagrams
\* Supports ICS02 (Clients).
UpdateChainStore(chainID, datagrams) == 
    
    \* ICS02: Client updates
    LET updatedStore == LightClientUpdate(chainID, chainStore, datagrams) IN

    \* update height
    IF /\ chainStore /= updatedStore
        /\ chainStore.height + 1 \in Heights 
    THEN [updatedStore EXCEPT !.height = chainStore.height + 1]
    ELSE updatedStore

(***************************************************************************
 Chain actions
 ***************************************************************************)       
\* Advance the height of the chain until MaxHeight is reached
AdvanceChain ==
    /\ chainStore.height + 1 \in Heights
    /\ chainStore' = [chainStore EXCEPT !.height = chainStore.height + 1]
    /\ UNCHANGED <<incomingDatagrams>>

\* Handle the datagrams and update the chain state        
HandleIncomingDatagrams ==
    /\ \/ incomingDatagrams /= AsSetDatagrams({})
    /\ LET updatedChainStore == UpdateChainStore(ChainID, incomingDatagrams) IN
        /\ chainStore' = updatedChainStore
        /\ incomingDatagrams' = AsSetDatagrams({})

(***************************************************************************
 Specification
 ***************************************************************************)
\* Initial state predicate
\* Initially
\*  - each chain is initialized to some element of the set
\*    InitChainStores (defined in IBCCoreDefinitions.tla)
\*  - pendingDatagrams for each chain is empty
Init == 
    /\ chainStore \in InitChainStore
    /\ incomingDatagrams = AsSetDatagrams({})

\* Next state action
\* The chain either
\*  - advances its height
\*  - receives datagrams and updates its state
Next ==
    \/ AdvanceChain 
    \/ HandleIncomingDatagrams
    \/ UNCHANGED vars
        
Fairness ==
    /\ WF_vars(AdvanceChain)
    /\ WF_vars(HandleIncomingDatagrams)        
        
(***************************************************************************
 Invariants
 ***************************************************************************)
\* Type invariant   
\* ChainStores, Datagrams are defined in IBCCoreDefinitions.tla        
TypeOK ==    
    /\ chainStore \in ChainStores(MaxHeight)
    /\ incomingDatagrams \in SUBSET Datagrams(MaxHeight)
    
(***************************************************************************
 Properties
 ***************************************************************************)    
\* it ALWAYS holds that the height of the chain does not EVENTUALLY decrease
HeightDoesntDecrease ==
    [](\A h \in Heights : chainStore.height = h 
        => <>(chainStore.height >= h))

=============================================================================
\* Modification History
\* Last modified Tue Dec 01 10:40:51 CET 2020 by ilinastoilkovska
\* Created Fri Jun 05 16:56:21 CET 2020 by ilinastoilkovska
